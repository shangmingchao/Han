plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.dicedmelon.gradle.jacoco-android'
}

def sdkPropertiesFile = rootProject.file("sdk.properties")
def sdkProperties = new Properties()
sdkProperties.load(new FileInputStream(sdkPropertiesFile))
def minSdkVersion = sdkProperties['minSdk'].toInteger()
def targetSdkVersion = sdkProperties['targetSdk'].toInteger()
def compileSdkVersion = sdkProperties['compileSdk'].toInteger()

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def versionPropertiesFile = rootProject.file("version.properties")
def versionProperties = new Properties()
versionProperties.load(new FileInputStream(versionPropertiesFile))
def versionMajor = versionProperties['versionMajor'].toInteger()
def versionMinor = versionProperties['versionMinor'].toInteger()
def versionPatch = versionProperties['versionPatch'].toInteger()
def versionBuild = versionProperties['versionBuild'].toInteger()

jacoco {
    toolVersion = "0.8.4"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

jacocoAndroidUnitTestReport {
    excludes += []
}

android {
    namespace 'com.frank.han'
    compileSdk compileSdkVersion

    defaultConfig {
        applicationId "com.frank.han"
        minSdk minSdkVersion
        targetSdk targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables {
            useSupportLibrary true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias "han"
            keyPassword keystoreProperties.containsKey("keyPassword") ? keystoreProperties['keyPassword'] : System.getenv("keyPassword")
            storeFile file("../han.keystore")
            storePassword keystoreProperties.containsKey("storePassword") ? keystoreProperties['storePassword'] : System.getenv("storePassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}_${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}.apk"
        }
    }
}

dependencies {
    implementation deps.androidx.appcompat
    implementation deps.androidx.appcompat_resources
    implementation deps.androidx.recyclerview
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.startup
    implementation deps.androidx.datastore_prefs
    implementation deps.androidx.datastore_proto
    implementation deps.androidx.ktx_core
    implementation deps.androidx.ktx_collection
    implementation deps.androidx.ktx_fragment
    implementation deps.androidx.ktx_lifecycle_viewmodel
    implementation deps.androidx.lifecycle_viewmodel_savedstate
    implementation deps.androidx.ktx_lifecycle_livedata
    implementation deps.androidx.ktx_navigation_fragment
    implementation deps.androidx.ktx_navigation_ui
    implementation deps.androidx.room_core
    implementation deps.androidx.room_ktx
    implementation deps.androidx.window
    def composeBom = platform(deps.compose_bom)
    implementation composeBom
    implementation deps.androidx.compose_material3
    implementation deps.androidx.compose_ui_tooling_preview
    implementation deps.androidx.compose_material_icons_core
    implementation deps.androidx.compose_material_icons_extended
    implementation deps.androidx.compose_material_window
    implementation deps.androidx.compose_activity
    implementation deps.androidx.compose_viewmodel
    implementation deps.androidx.compose_livedata
    ksp deps.androidx.room_compiler

    implementation deps.retrofit.core
    implementation deps.retrofit.converter_gson

    implementation deps.okhttp.logging_interceptor

    implementation deps.koin.core
    implementation deps.koin.android
    implementation deps.koin.workmanager
    implementation deps.koin.navigation
    implementation deps.koin.compose

    implementation deps.stetho.core
    implementation deps.stetho.okhttp3_helper

    implementation deps.glide.core
    ksp deps.glide.compiler

    testImplementation deps.androidx_test.arch_core
    testImplementation deps.androidx_test.junit_ktx
    testImplementation deps.kotlinx_coroutines_test
    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.robolectric
    testImplementation deps.google_truth
    testImplementation deps.retrofit.mock
    testImplementation deps.androidx_test.room
    testImplementation deps.koin.test

    androidTestImplementation deps.androidx_test.core
    androidTestImplementation deps.androidx_test.core_ktx
    androidTestImplementation deps.androidx_test.navigation
    androidTestImplementation deps.androidx_test.runner
    androidTestImplementation deps.androidx_test.rules
    androidTestImplementation deps.androidx_test.junit
    androidTestImplementation deps.androidx_test.junit_ktx
    androidTestImplementation deps.androidx_test.espresso_core
    androidTestImplementation deps.androidx_test.espresso_contrib
    androidTestImplementation deps.androidx_test.espresso_intents
    androidTestImplementation deps.google_truth
    androidTestImplementation composeBom
    androidTestImplementation deps.androidx_test.compose_junit4

    debugImplementation deps.androidx_test.core
    debugImplementation deps.androidx_test.fragment
    debugImplementation  deps.androidx_test.compose_ui_tooling
    debugImplementation  deps.androidx_test.compose_ui_test_manifest
}
